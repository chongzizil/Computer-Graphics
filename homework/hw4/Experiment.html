<script src="homework/lib/lib1.js"></script>

<div id="canvas-container">
  <h2 class="sub-title">Wood Texture</h2>

  <canvas id='canvas1' width=600 height=600></canvas>
</div>

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>
  precision mediump float;
  uniform float uTime;
  uniform vec3  uCursor;
  varying vec3  vPosition;

  const float PI = 3.1415926535897932384626433832795;
  const float NOT_FOUND = 1000.;
  const float ambientVar = 1. / 5.;
  const float diffuseVar = 1. / .75;
  const float refrectVar = 1.23;

  const int numOfSpheres = 3;
  vec4 sphere[numOfSpheres];
  vec3 material[numOfSpheres];
  float sphereFractionIndex[numOfSpheres];

  const int numOfLights = 1;
  vec3 Lrgb[numOfLights];
  vec3 Ldir[numOfLights];

  vec4 foundSphere;
  vec3 foundMaterial;
  float foundFractionIndex;
  float foundT;

  vec3 reflectRay;
  vec3 refrectRay;

  // All colors come from: http://nipponcolors.com
  const int numOfColors = 10;
  vec3 colors[10];
  // RGB
  const vec3 COLOR_KURENAI = vec3(203. / 255., 27. / 255., 69. / 255.);
  const vec3 COLOR_USUKI = vec3(250. / 255., 214. / 255., 137. / 255.);
  const vec3 COLOR_MIZU = vec3(129. / 255., 199. / 255., 212. / 255.);
  const vec3 COLOR_SAKURA = vec3(254. / 255., 223. / 255., 225. / 255.);
  const vec3 COLOR_YAMABUKI = vec3(255. / 255., 177. / 255., 27. / 255.);
  const vec3 COLOR_RURIKON = vec3(11. / 255., 52. / 255., 110. / 255.);
  // Other
  const vec3 COLOR_NAE = vec3(134. / 255., 193. / 255., 102. / 255.);
  const vec3 COLOR_FUJI = vec3(139. / 255., 129. / 255., 195. / 255.);
  // Black White
  const vec3 COLOR_SHIRONERI = vec3(252. / 255., 250. / 255., 242. / 255.);
  const vec3 COLOR_RO = vec3(12. / 255., 12. / 255., 12. / 255.);

    // noise
  vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
  vec4 mod289(vec4 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
  vec4 permute(vec4 x) { return mod289(((x*34.0)+1.0)*x); }
  vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
  vec3 fade(vec3 t) { return t*t*t*(t*(t*6.0-15.0)+10.0); }

  float noise(vec3 P) {
     vec3 i0 = mod289(floor(P)), i1 = mod289(i0 + vec3(1.0));
     vec3 f0 = fract(P), f1 = f0 - vec3(1.0), f = fade(f0);
     vec4 ix = vec4(i0.x, i1.x, i0.x, i1.x), iy = vec4(i0.yy, i1.yy);
     vec4 iz0 = i0.zzzz, iz1 = i1.zzzz;
     vec4 ixy = permute(permute(ix) + iy), ixy0 = permute(ixy + iz0), ixy1 = permute(ixy + iz1);
     vec4 gx0 = ixy0 * (1.0 / 7.0), gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
     vec4 gx1 = ixy1 * (1.0 / 7.0), gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
     gx0 = fract(gx0); gx1 = fract(gx1);
     vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0), sz0 = step(gz0, vec4(0.0));
     vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1), sz1 = step(gz1, vec4(0.0));
     gx0 -= sz0 * (step(0.0, gx0) - 0.5); gy0 -= sz0 * (step(0.0, gy0) - 0.5);
     gx1 -= sz1 * (step(0.0, gx1) - 0.5); gy1 -= sz1 * (step(0.0, gy1) - 0.5);
     vec3 g0 = vec3(gx0.x,gy0.x,gz0.x), g1 = vec3(gx0.y,gy0.y,gz0.y),
          g2 = vec3(gx0.z,gy0.z,gz0.z), g3 = vec3(gx0.w,gy0.w,gz0.w),
          g4 = vec3(gx1.x,gy1.x,gz1.x), g5 = vec3(gx1.y,gy1.y,gz1.y),
          g6 = vec3(gx1.z,gy1.z,gz1.z), g7 = vec3(gx1.w,gy1.w,gz1.w);
     vec4 norm0 = taylorInvSqrt(vec4(dot(g0,g0), dot(g2,g2), dot(g1,g1), dot(g3,g3)));
     vec4 norm1 = taylorInvSqrt(vec4(dot(g4,g4), dot(g6,g6), dot(g5,g5), dot(g7,g7)));
     g0 *= norm0.x; g2 *= norm0.y; g1 *= norm0.z; g3 *= norm0.w;
     g4 *= norm1.x; g6 *= norm1.y; g5 *= norm1.z; g7 *= norm1.w;
     vec4 nz = mix(vec4(dot(g0, vec3(f0.x, f0.y, f0.z)), dot(g1, vec3(f1.x, f0.y, f0.z)),
                        dot(g2, vec3(f0.x, f1.y, f0.z)), dot(g3, vec3(f1.x, f1.y, f0.z))),
                   vec4(dot(g4, vec3(f0.x, f0.y, f1.z)), dot(g5, vec3(f1.x, f0.y, f1.z)),
                        dot(g6, vec3(f0.x, f1.y, f1.z)), dot(g7, vec3(f1.x, f1.y, f1.z))), f.z);
     return 2.2 * mix(mix(nz.x,nz.z,f.y), mix(nz.y,nz.w,f.y), f.x);
  }

  float fractal(vec3 P) {
    float res = 0.;
    for (int j = 1; j < 25; j++) {
      res += noise(pow(2., float(j)) * P) / pow(2., float(j));
    }
    return res;
  }

  float turbulence(vec3 P) {
    float res = 0.;
    for (int j = 1; j < 32; j++) {
      res += abs(noise(pow(2., float(j)) * P) / pow(2., float(j)));
    }
    return res;
  }

  float iceSphere(vec3 P) {
    float res = 0.;
    for (int j = 1; j < 50; j++) {
      res += (1. / float(j)) * noise(float(j) * P);
    }
    return res;
  }

  float fireSphere(vec3 P) {
    float res = 0.;
    for (int j = 1; j < 50; j++) {
      res += (1. / float(j)) * abs(noise(float(j) * P));
    }
    return res;
  }

  void setColors() {
    colors[0] = COLOR_KURENAI;
    colors[1] = COLOR_USUKI;
    colors[2] = COLOR_MIZU;
    colors[3] = COLOR_SAKURA;
    colors[4] = COLOR_YAMABUKI;
    colors[5] = COLOR_RURIKON;
    colors[6] = COLOR_NAE;
    colors[7] = COLOR_FUJI;
    colors[8] = COLOR_SHIRONERI;
    colors[9] = COLOR_RO;
  }

  void setLights(vec2 c) {
    Lrgb[0] = vec3(1., 1., 1.);
    //Lrgb[1] = vec3(1., 1., 1.);
    // Lrgb[2] = vec3(1., 1., 1.);
    // Lrgb[3] = vec3(1., 1., 1.);

    Ldir[0] = vec3(c.x, c.y, 1. - 2. * dot(c, c));
    // Ldir[1] = vec3(c.x, c.y, 1. - 2. * dot(c, c));
    // Ldir[2] = vec3(-1., -1., 1. - 2. * dot(c, c));
    // Ldir[3] = vec3(1., -1., 1. - 2. * dot(c, c));

    for (int i = 0; i < numOfLights; i++) {
      Ldir[i] = normalize(Ldir[i]);
    }
  }

  void setMaterial() {
    material[0] = vec3(110. / 255., 81. / 255., 30. / 255.);
    material[1] = colors[0];
    material[2] = colors[2];
    for (int i = 3; i < numOfSpheres; i++) {
      material[i] = colors[int(mod(float(i), float(numOfColors)))];
    }
  }

  void setSphereMovements() {
    sphere[0] = vec4(0., 0., -2.3, .50);

    sphere[1] = vec4(.7 * cos(uTime), 0., -2.3 + .7 * sin(uTime), .10);
    sphere[2] = vec4(0., .7 * cos(uTime + PI), -2.3 + .7 * sin(uTime + PI), .10);

    // sphere[6] = vec4(cos(1. * PI / 5.) * .9 * cos(uTime + 0. * PI / 2.), sin(1. * PI / 5.) * .9 * cos(uTime + 0. * PI / 2.), -2.3 + .9 * sin(uTime + 0. * PI / 2.), .10);
    // sphere[5] = vec4(cos(2. * PI / 5.) * .9 * cos(uTime + 3. * PI / 2.), sin(2. * PI / 5.) * .9 * cos(uTime + 3. * PI / 2.), -2.3 + .9 * sin(uTime + 3. * PI / 2.), .10);
    // sphere[4] = vec4(cos(3. * PI / 5.) * .9 * cos(uTime + 1. * PI / 2.), sin(3. * PI / 5.) * .9 * cos(uTime + 1. * PI / 2.), -2.3 + .9 * sin(uTime + 1. * PI / 2.), .10);
    // sphere[3] = vec4(cos(4. * PI / 5.) * .9 * cos(uTime + 2. * PI / 2.), sin(4. * PI / 5.) * .9 * cos(uTime + 2. * PI / 2.), -2.3 + .9 * sin(uTime + 2. * PI / 2.), .10);

    for (int i = 0; i < numOfSpheres; i++) {
      sphereFractionIndex[i] = refrectVar;
    }
  }

  // Compute intersection of a ray with a sphere, if any. Store the sphere,
  // material and t for later usage.
  // If there is no intersection, return default t which is 1000.
  void findSphere(vec3 V, vec3 W) {
    foundT = NOT_FOUND;

    for (int i = 0; i < numOfSpheres; i++) {
      float B = 2. * dot(W, V - sphere[i].xyz);
      float C = dot(V - sphere[i].xyz, V - sphere[i].xyz) - pow(sphere[i].w, 2.);
      float D = pow(B, 2.) - 4. * C;

      if (D < 0.) {
        continue;
      }

      float t = (-B - sqrt(D)) / 2.;

      if (t >= 0. && t < foundT) {
        foundT = t;
        foundSphere = sphere[i];
        foundMaterial = material[i];
        foundFractionIndex = sphereFractionIndex[i];
      }
    }
  }

  void findExit(vec3 V, vec3 W) {
    foundT = NOT_FOUND;

    for (int i = 0; i < numOfSpheres; i++) {
      float B = 2. * dot(W, V - sphere[i].xyz);
      float C = dot(V - sphere[i].xyz, V - sphere[i].xyz) - pow(sphere[i].w, 2.);
      float D = pow(B, 2.) - 4. * C;

      if (D < 0.) {
        continue;
      }

      float t = (-B + sqrt(D)) / 2.;

      if (t >= 0. && t < foundT) {
        foundT = t;
        foundSphere = sphere[i];
        foundMaterial = material[i];
        foundFractionIndex = sphereFractionIndex[i];
      }
    }
  }

  vec3 findReflection(vec3 point, vec3 N, vec3 W) {
    vec3 rW = normalize(2. * dot(N, -W) * N - (-W));

    findSphere(point + .001 * rW, rW);

    point = point + foundT * rW;
    N = normalize((point - foundSphere.xyz) / foundSphere.w);

    reflectRay = -rW;

    return N;
  }

  vec3 findRefraction(vec3 point, vec3 N, vec3 W) {
    float fractionIndex = foundFractionIndex;
    vec3 newW = refract(W, N, fractionIndex);

    findExit(point, newW);

    point = point + foundT * newW;
    //N = normalize((point - foundSphere.xyz) / foundSphere.w);

    newW = refract(newW, N, fractionIndex);
    //TODO: Fix the refraction
    findSphere(point + newW * .01, newW);

    point = point + foundT * newW;
    N = normalize((point - foundSphere.xyz) / foundSphere.w);

    refrectRay = -newW;

    return N;
  }

  vec3 shadeSphere(vec3 point, vec4 sphere, vec3 material, vec3 W) {
    vec3 N = normalize((point - sphere.xyz) / sphere.w);

    vec3 noiseP = point - sphere.xyz;
    float dist = sqrt(pow(noiseP.x, 2.) + pow(noiseP.y, 2.)) + .15 * turbulence(noiseP);
    float cc = sin(2. * 24. * dist * PI * (mod(uTime, 25.) * .04 + .2));
    material += material * cc;
    // material = vec3(material.r * cc, material.g * cc, material.b);

    vec3 ambient = material * ambientVar;
    vec3 diffuse = material * diffuseVar;

    vec3 color = ambient;

    for (int i = 0; i < numOfLights; i++) {
      //Color

      vec3 H = (N + Ldir[i]) * .5; 
      color += Lrgb[i] * (diffuse * max(0., dot(N, Ldir[i])) + material * pow(max(0., dot(N, H)), 75.));

      // Check reflection
      vec3 reflectN = findReflection(point, N, W);
      if (foundT != NOT_FOUND) {
        vec3 ambient = foundMaterial * ambientVar;
        vec3 diffuse = foundMaterial * diffuseVar;

        vec3 reflectH = (reflectN + Ldir[i]) * .5; 
        vec3 reflectColor = Lrgb[i] * (diffuse * max(0., dot(reflectN, reflectRay)) + material * pow(max(0., dot(reflectN, reflectH)), 75.));

        color += reflectColor;
      }

      // Check refraction
      vec3 refrectN = findRefraction(point, N, W);
      if (foundT != NOT_FOUND) {
        vec3 ambient = foundMaterial * ambientVar;
        vec3 diffuse = foundMaterial * diffuseVar;

        vec3 refractH = (refrectN + Ldir[i]) * .5; 
        vec3 refractColor = Lrgb[i] * (diffuse * max(0., dot(refrectN, refrectRay)) + material * pow(max(0., dot(refrectN, refractH)), 75.));

        color += refractColor;
      }
    }

    return color;
  }

  void main(void) {
    vec2 c = uCursor.xy;

    // Set the default colors
    setColors();

    // Set sphere material
    setMaterial();

    // Set the lights
    setLights(c);

    // Set V and W
    vec3 V = vec3(0., 0., 0.);
    vec3 W = normalize(vec3(vPosition.x, vPosition.y, -2.));

    // Set sphere movements
    setSphereMovements();

    // Set background color
    vec3 color = vec3(0., 0., 0.);

    // Find if there's any spheres
    findSphere(V, W);

    // No sphere
    if (foundT == NOT_FOUND) {
      gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.
      return;
    }

    // Shade shade...
    color = shadeSphere(V + foundT * W, foundSphere, foundMaterial, W);
    gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.
  }
</script>

<script>
    start_gl('canvas1', document.getElementById('my_vertex_shader'  ).innerHTML,
            document.getElementById('my_fragment_shader').innerHTML);
</script>