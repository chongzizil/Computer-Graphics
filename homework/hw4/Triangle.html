<h1>Triangle</h1>

<body bgcolor=black>
<td><canvas id='canvas1' width=600 height=600></canvas></td>
</body>

<script id='my_vertex_shader' type='x-shader/x-vertex'>
   attribute vec3 aPosition;
   varying   vec3 vPosition;
   void main() {
      gl_Position = vec4(aPosition, 1.0);
      vPosition = aPosition;
   }
</script>

<script id='my_fragment_shader' type='x-shader/x-fragment'>
  precision mediump float;
  uniform float uTime;
  uniform vec3  uCursor;
  varying vec3  vPosition;

  const float PI = 3.1415926535897932384626433832795;
  const float NOT_FOUND = 1000.;
  const float ambientVar = 1. / 5.;
  const float diffuseVar = 1. / .75;
  const float refrectVar = 1.23;

  const int numOfSpheres = 4;
  vec4 sphere[numOfSpheres];
  vec3 material[numOfSpheres];
  float sphereFractionIndex[numOfSpheres];

  vec4 foundSphere;
  vec3 foundMaterial;
  float foundFractionIndex;
  float foundT;

  vec3 reflectRay;
  vec3 refrectRay;

  const int numOfLights = 1;
  vec3 Lrgb[numOfLights];
  vec3 Ldir[numOfLights];

  float numOfTriangles = 3.;
  float triangle_D = -4.;
  float foundTriangleIndex = -1.;
  float foundTriangleT = -1.;
  vec3 foundTriangleMaterial;
  vec3 triangleA[3];
  vec3 triangleB[3];
  vec3 triangleC[3];
  vec3 triangle_material[3];

  // All colors come from: http://nipponcolors.com
  const int numOfColors = 10;
  vec3 colors[10];
  // RGB
  const vec3 COLOR_KURENAI = vec3(203. / 255., 27. / 255., 69. / 255.);
  const vec3 COLOR_USUKI = vec3(250. / 255., 214. / 255., 137. / 255.);
  const vec3 COLOR_MIZU = vec3(129. / 255., 199. / 255., 212. / 255.);
  const vec3 COLOR_SAKURA = vec3(254. / 255., 223. / 255., 225. / 255.);
  const vec3 COLOR_YAMABUKI = vec3(255. / 255., 177. / 255., 27. / 255.);
  const vec3 COLOR_RURIKON = vec3(11. / 255., 52. / 255., 110. / 255.);
  // Other
  const vec3 COLOR_NAE = vec3(134. / 255., 193. / 255., 102. / 255.);
  const vec3 COLOR_FUJI = vec3(139. / 255., 129. / 255., 195. / 255.);
  // Black White
  const vec3 COLOR_SHIRONERI = vec3(252. / 255., 250. / 255., 242. / 255.);
  const vec3 COLOR_RO = vec3(12. / 255., 12. / 255., 12. / 255.);

  void setColors() {
    colors[0] = COLOR_KURENAI;
    colors[1] = COLOR_USUKI;
    colors[2] = COLOR_MIZU;
    colors[3] = COLOR_SAKURA;
    colors[4] = COLOR_YAMABUKI;
    colors[5] = COLOR_RURIKON;
    colors[6] = COLOR_NAE;
    colors[7] = COLOR_FUJI;
    colors[8] = COLOR_SHIRONERI;
    colors[9] = COLOR_RO;
  }

  void setLights(vec2 c) {
    Lrgb[0] = vec3(1., 1., 1.);
    Ldir[0] = vec3(c.x, c.y, 1. - 2. * dot(c, c));

    for (int i = 0; i < numOfLights; i++) {
      Ldir[i] = normalize(Ldir[i]);
    }
  }

  void setMaterial() {
    for (int i = 0; i < numOfSpheres; i++) {
      material[i] = colors[int(mod(float(i + 4), float(numOfColors)))];
    }
  }

  void setSphereMovements() {
    sphere[0] = vec4(cos(uTime) * .4, sin(uTime) * .4, -5., .3);
    sphere[1] = vec4(cos(uTime) * 1.2, sin(uTime) * 1.2, -6., .3);
    sphere[2] = vec4(cos(uTime) * 2.2, sin(uTime) * 2.2, -7., .3);
    sphere[3] = vec4(uCursor.x * 2.5, uCursor.y * 2.5, -5., .3);

    for (int i = 0; i < numOfSpheres; i++) {
      sphereFractionIndex[i] = refrectVar;
    }
  }

  void setTriangle() {
    triangleA[0] = vec3(0., 0., -4.);
    triangleA[1] = vec3(-sqrt(2.), -sqrt(2.), -4.);
    triangleA[2] = vec3(0., 2., -4.);

    triangleB[0] = vec3(0., 0., -4.);
    triangleB[1] = vec3(sqrt(2.), -sqrt(2.), -4.);
    triangleB[2] = vec3(0., 2., -4.);

    triangleC[0] = vec3(0., 0., -4.);
    triangleC[1] = vec3(-sqrt(2.), -sqrt(2.), -4.);
    triangleC[2] = vec3(sqrt(2.), -sqrt(2.), -4.);

    triangle_material[0] = colors[0];
    triangle_material[1] = colors[1];
    triangle_material[2] = colors[2];
  }

  // Compute intersection of a ray with a sphere, if any. Store the sphere,
  // material and t for later usage.
  // If there is no intersection, return default t which is 1000.
  void findSphere(vec3 V, vec3 W) {
    foundT = NOT_FOUND;

    for (int i = 0; i < numOfSpheres; i++) {
      float B = 2. * dot(W, V - sphere[i].xyz);
      float C = dot(V - sphere[i].xyz, V - sphere[i].xyz) - pow(sphere[i].w, 2.);
      float D = pow(B, 2.) - 4. * C;

      if (D < 0.) {
        continue;
      }

      float t = (-B - sqrt(D)) / 2.;

      if (t >= 0. && t < foundT) {
        foundT = t;
        foundSphere = sphere[i];
        foundMaterial = material[i];
        foundFractionIndex = sphereFractionIndex[i];
      }
    }
  }

  void findExit(vec3 V, vec3 W) {
    foundT = NOT_FOUND;

    for (int i = 0; i < numOfSpheres; i++) {
      float B = 2. * dot(W, V - sphere[i].xyz);
      float C = dot(V - sphere[i].xyz, V - sphere[i].xyz) - pow(sphere[i].w, 2.);
      float D = pow(B, 2.) - 4. * C;

      if (D < 0.) {
        continue;
      }

      float t = (-B + sqrt(D)) / 2.;

      if (t >= 0. && t < foundT) {
        foundT = t;
        foundSphere = sphere[i];
        foundMaterial = material[i];
        foundFractionIndex = sphereFractionIndex[i];
      }
    }
  }

  vec3 findReflection(vec3 point, vec3 N, vec3 W) {
    vec3 rW = normalize(2. * dot(N, -W) * N - (-W));

    findSphere(point + .001 * rW, rW);

    point = point + foundT * rW;
    N = normalize((point - foundSphere.xyz) / foundSphere.w);

    reflectRay = -rW;

    return N;
  }

  vec3 findRefraction(vec3 point, vec3 N, vec3 W) {
    float fractionIndex = foundFractionIndex;
    vec3 newW = refract(W, N, fractionIndex);

    findExit(point, newW);

    point = point + foundT * newW;
    findSphere(point + newW * .01, newW);

    point = point + foundT * newW;
    N = normalize((point - foundSphere.xyz) / foundSphere.w);

    refrectRay = -newW;

    return N;
  }

  vec3 shadeSphere(vec3 point, vec4 sphere, vec3 material, vec3 W) {
    vec3 N = normalize((point - sphere.xyz) / sphere.w);

    vec3 ambient = material * ambientVar;
    vec3 diffuse = material * diffuseVar;

    vec3 color = ambient;

    for (int i = 0; i < numOfLights; i++) {
      vec3 H = (N + Ldir[i]) * .5; 
      color += Lrgb[i] * (diffuse * max(0., dot(N, Ldir[i])) + material * pow(max(0., dot(N, H)), 75.));

      // Check reflection
      vec3 reflectN = findReflection(point, N, W);
      if (foundT != NOT_FOUND) {
        vec3 ambient = foundMaterial * ambientVar;
        vec3 diffuse = foundMaterial * diffuseVar;

        vec3 reflectH = (reflectN + Ldir[i]) * .5; 
        vec3 reflectColor = Lrgb[i] * (diffuse * max(0., dot(reflectN, reflectRay)) + material * pow(max(0., dot(reflectN, reflectH)), 75.));

        color += reflectColor;
      }

      // Check refraction
      vec3 refrectN = findRefraction(point, N, W);
      if (foundT != NOT_FOUND) {
        vec3 ambient = foundMaterial * ambientVar;
        vec3 diffuse = foundMaterial * diffuseVar;

        vec3 refractH = (refrectN + Ldir[i]) * .5; 
        vec3 refractColor = Lrgb[i] * (diffuse * max(0., dot(refrectN, refrectRay)) + material * pow(max(0., dot(refrectN, refractH)), 75.));

        color += refractColor;
      }
    }

    return color;
  }

  float computeT(vec3 V, vec3 W, vec3 N, float d) {
    return -(dot(N, V) + d) / dot(N, W);
  }

  float computeTW(vec4 plain, vec3 W) {
    return dot(W, plain.xyz);
  }

  float getArea(vec3 A, vec3 B) {
    return .5 * length(cross(A, B));
  }

  void findTriangle(vec3 triangle[3], vec3 V, vec3 W, float i) {
    vec3 N = normalize(vec3(0., 0., -1));
    vec3 vectex0 = triangle[0];
    vec3 vectex1 = triangle[1];
    vec3 vectex2 = triangle[2];
    
    float t = computeT(V, W, N, triangle_D);
    vec3 P = V + t * W;
    float AreaTotal = getArea(vectex1 - vectex0, vectex2 - vectex0);
    float AreaY = getArea(vectex0 - P, vectex1 - P);
    float AreaA = getArea(vectex1 - P, vectex2 - P);
    float AreaB = getArea(vectex0 - P, vectex2 - P);
    float A = AreaA / AreaTotal;
    float B = AreaB / AreaTotal;
    float Y = AreaY / AreaTotal;

    if ((A >= 0. && A <= 1.) && (B >= 0. && B <= 1.) && (Y >= 0. && Y <= 1.) && A + B + Y >= .99999 && A + B + Y <= 1.00001) {
      foundTriangleIndex = i;
      foundTriangleT = t;
    }
  }

  void findTriangles(vec3 V, vec3 W) {
    findTriangle(triangleA, V, W, 0.);
    findTriangle(triangleB, V, W, 1.);
    findTriangle(triangleC, V, W, 2.);
  }

  void main(void) {
    vec2 c = uCursor.xy;

    // Set the default colors
    setColors();

    // Set sphere material
    setMaterial();

    // Set the lights
    setLights(c);

    // Set V and W
    vec3 V = vec3(0., 0., 0.);
    vec3 W = vec3(vPosition.x, vPosition.y, -2.);

    // Set sphere movements
    setSphereMovements();

    setTriangle();

    // Set background color
    vec3 color = vec3(0., 0., 0.);

    // findCube(V, W);
    findTriangles(V, W);

    if (foundTriangleIndex != -1.) {
      W = normalize(W);
      vec3 point = V + foundTriangleT * W;
      vec3 N = vec3(0., 0., 1.);

      vec3 material;
      if (foundTriangleIndex == 0.) {
        material = triangle_material[0];
        N = vec3(1., -1., 1.);
      } else if (foundTriangleIndex == 1.) {
        material = triangle_material[0];
        N = vec3(1., 1., 1.);
      } else {
        material = triangle_material[0];
        N = vec3(0., 1., 1.);
      }
      
      vec3 ambient = material * ambientVar;
      vec3 diffuse = material * diffuseVar;

      color = ambient;

      vec3 H = (N + Ldir[0]) * .5; 
      color += Lrgb[0] * (diffuse * max(0., dot(N, Ldir[0])));

      // Shpere
      float fractionIndex;

      if (foundTriangleIndex == 0.) {
        fractionIndex = .9;
      } else if (foundTriangleIndex == 1.) {
        fractionIndex = 1.;
      } else {
        fractionIndex = 1.1;
      }
      // fractionIndex = 1.;

      vec3 newW = refract(W, N, fractionIndex);
      findSphere(point + newW * .01, newW);
      point = point + foundT * newW;

      if (foundT != NOT_FOUND) {
        color += shadeSphere(point, foundSphere, foundMaterial, normalize(newW));
      }
    }

    // Find if there's any spheres
    findSphere(V, normalize(W));

    if (foundT == NOT_FOUND) {
      gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.
      return;
    }

    if (foundTriangleIndex == -1.) {
      color = shadeSphere(V + foundT * normalize(W), foundSphere, foundMaterial, normalize(W));
    }

    gl_FragColor = vec4(sqrt(color), 1.); // Do Gamma correction.
  }
</script>

<script>
  start_gl('canvas1', document.getElementById('my_vertex_shader'  ).innerHTML,
      document.getElementById('my_fragment_shader').innerHTML);
</script>